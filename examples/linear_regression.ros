functie eroare_mse(var y_true, var y_pred) {
    var nn = lungime(y_true);
    var suma = 0.0;
    pentru (var i = 0; i < nn; i = i + 1) {
        var diff = y_pred[i] - y_true[i];
        suma = suma + diff * diff;
    }
    returneaza suma / nn;
}

functie prezice(var x, var w, var b) {
    var m = lungime(x);
    var n = lungime(w);

    var rezultate = [];

    pentru (var i = 0; i < m; i = i + 1) {
        var y_pred = 0.0;
        pentru (var j = 0; j < n; j = j + 1) {
            y_pred = y_pred + w[j] * x[i,j];
        }
        y_pred = y_pred + b;
        adauga(rezultate, y_pred);
    }

    returneaza rezultate;
}

functie regresie_liniara(var x, var y, var invatare, var epoci) {
    var n = lungime(x[0]);
    var m = lungime(x);
    
    var w = [];
    pentru (var i = 0; i < n; i = i + 1) {
        adauga(w, 0.0);
    }

    var b = 0.0;

    pentru (var ep = 0; ep < epoci; ep++) {
        var dw = [];
        pentru (var i = 0; i < n; i = i + 1) {
            adauga(dw, 0.0);
        }

        var db = 0.0;

        pentru (var i = 0; i < m; i = i + 1) {
            var y_pred = 0.0;
            pentru (var j = 0; j < n; j = j + 1) {
                y_pred = y_pred + w[j] * x[i,j];
            }
            y_pred = y_pred + b;

            var eroare = y_pred - y[i];
            pentru (var j = 0; j < n; j = j + 1) {
                dw[j] = dw[j] + eroare * x[i,j];
            }
            db = db + eroare;
        }

        pentru (var j = 0; j < n; j = j + 1) {
            w[j] = w[j] - invatare * dw[j] / m;
        }
        b = b - invatare * db / m;

        daca (ep % 100 == 0) {
            var y_pred_tot = prezice(x, w, b);
            var loss = eroare_mse(y, y_pred_tot);
            afiseaza("Epoch ", ep, " Loss: ", loss, "\n");
        }
    }

    returneaza [w,b];
}

var x = [
    [1, 2, 3],   // sample 1 with 3 features
    [2, 3, 4],   // sample 2
    [4, 5, 6]    // sample 3
];

var y = [14, 20, 32];  // target outputs for each sample

var model = regresie_liniara(x, y, 0.01, 3000);

var x_nou = [
    [1, 2, 3],
    [2, 1, 0],
    [0, 0, 0]
];

var rezultate = prezice(x_nou, model[0], model[1]);
afiseaza(rezultate);